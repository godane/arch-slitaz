# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

pkgname=busybox
pkgver=1.16.1
pkgrel=1as
pkgdesc="Utilities for rescue and embedded systems"
arch=("i686" "x86_64")
url="http://busybox.net"
license=('GPL')
depends=("slitaz-base-files" "glibc")
makedepends=("make" "gcc" "sed" "ncurses")
options=()
source=($url/downloads/$pkgname-$pkgver.tar.bz2 \
	$pkgname-$pkgver.config
	busybox.conf
	dnsd.conf
	inetd.conf
	udhcp.script
	udhcpd.conf
	zcip.script
	daemon
	$pkgname-$pkgver-vcsa2txt.u
	$pkgname-$pkgver-cpio-mkdir.u
	$pkgname-$pkgver-tar.u
	$pkgname-$pkgver-stat.u
	$pkgname-$pkgver-syslogd.u
	$pkgname-$pkgver-basename.u
	$pkgname-$pkgver-paths.u)
md5sums=('319486ec65078d07fde26eb620fecde7'
         '52abccb002560edb5b4e269870863e99'
         '7e0621b6b9855c5bc80b20928b367dbf'
         'f3fa6a2d2d95cf5eeb4297fec92630e1'
         '2a7f2d8b6befadca48309db2561ffe26'
         '2f099ea6f893fb8f970f7d5acb9bac4b'
         '667803a8d21a97e8e0a969ddab9fae65'
         '54cb91c9b4d687bd5c24faab817d82ec'
         '79d1b571ba7f0faddd9063256775fe4e'
         'a0b64dea4c2cf47fea2414bfb36d08a8'
         'ecb3ccd05ae4e45c2196e34f30fa9f5f'
         '2b2b32eca760af1c19f6616275fee606'
         'ff0a33ff7872cac07573497bf4fa2f9a'
         '6b90a770dfa11428c150c5cad1d01c37'
         '68d93b4abbfd4f62c8d6e6029de83edf'
         '997b5896a547ff1f227a47e5cbe49e0a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cp $srcdir/$pkgname-$pkgver.config .config
  patch -Np1 -i $srcdir/$pkgname-$pkgver-vcsa2txt.u || return 1
  patch -Np1 -i $srcdir/$pkgname-$pkgver-cpio-mkdir.u || return 1
  patch -Np1 -i $srcdir/$pkgname-$pkgver-tar.u || return 1
  patch -Np1 -i $srcdir/$pkgname-$pkgver-stat.u || return 1
  patch -Np1 -i $srcdir/$pkgname-$pkgver-syslogd.u || return 1
  patch -Np1 -i $srcdir/$pkgname-$pkgver-basename.u || return 1
  patch -Np1 -i $srcdir/$pkgname-$pkgver-paths.u || return 1
  
  # if you want to run menuconfig uncomment the following line:
  #make menuconfig ; return 1
  make || return 1
  make install || return 1
  chmod 4755 _install/bin/busybox
}

package() {
  cp -a $srcdir/$pkgname-$pkgver/_install/* $pkgdir
  rm -f $pkgdir/sbin/depmod $pkgdir/usr/bin/ar
  # Busybox config files
  mkdir -p $pkgdir/etc
  cp $srcdir/busybox.conf $pkgdir/etc
  chmod 600 $pkgdir/etc/busybox.conf
  cp $srcdir/dnsd.conf $pkgdir/etc
  cp $srcdir/udhcpd.conf $pkgdir/etc
  touch $pkgdir/etc/resolv.conf
  cp $srcdir/inetd.conf $pkgdir/etc
  mkdir -p $pkgdir/etc/init.d
  cp $srcdir/daemon $pkgdir/etc/init.d
  for i in dnsd inetd udhcpd zcip crond ; do
      ln -s $i $pkgdir/etc/init.d/$i
  done
  rm $pkgdir/linuxrc
  mkdir -p $pkgdir/etc/modprobe.d
  # Udhcpd stuff
  mkdir -p $pkgdir/usr/share/udhcpc
  cp $srcdir/udhcp.script $pkgdir/usr/share/udhcpc/default.script
  chmod +x $pkgdir/usr/share/udhcpc/default.script
  # ZeroConf stuff.
  cp $srcdir/zcip.script $pkgdir/etc
}
