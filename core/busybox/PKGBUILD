# $Id: PKGBUILD 82 2009-07-17 19:56:55Z aaron $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

pkgname=busybox
pkgver=1.17.1
pkgrel=1as
pkgdesc="Utilities for rescue and embedded systems"
arch=("i686" "x86_64")
url="http://busybox.net"
license=('GPL')
depends=("slitaz-base-files" "glibc")
makedepends=("make" "gcc" "sed" "ncurses")
options=()
source=($url/downloads/$pkgname-$pkgver.tar.bz2 \
	stuff/$pkgname-$pkgver.config
	stuff/busybox.conf
	stuff/dnsd.conf
	stuff/inetd.conf
	stuff/udhcp.script
	stuff/udhcpd.conf
	stuff/zcip.script
	stuff/daemon
	stuff/$pkgname-$pkgver-cpio-mkdir.u
	stuff/$pkgname-$pkgver-tar.u
	stuff/$pkgname-$pkgver-stat.u
	stuff/$pkgname-$pkgver-ris.u
	stuff/$pkgname-$pkgver-depmod.u
	stuff/$pkgname-$pkgver-zmodules.u
	stuff/$pkgname-$pkgver-usage.u)
md5sums=('319486ec65078d07fde26eb620fecde7'
         '98070dbd560d17753abb356521add504'
         '7e0621b6b9855c5bc80b20928b367dbf'
         'f3fa6a2d2d95cf5eeb4297fec92630e1'
         '2a7f2d8b6befadca48309db2561ffe26'
         '2f099ea6f893fb8f970f7d5acb9bac4b'
         '667803a8d21a97e8e0a969ddab9fae65'
         '54cb91c9b4d687bd5c24faab817d82ec'
         '79d1b571ba7f0faddd9063256775fe4e'
         '2a9f0cdf9dc747549b55caff210fdd55'
         'ff24cba29b5d77e9592282766c7ed0c9'
         '7893f846873e94256a7e6138b76fe002'
         '806cd508222b816ccbe9a34bdd724407'
         'e99c8b4a5c7704b49a75326f48c88983'
         '7d384ad9b33370f33f98fbedaf9aa892'
         '2c91fe40c7afdcf8d9712b5a6cbb83ea'
         '7b684742aba5ce11a437b73a38d02a84'
         '7dc100a83de04baf90586efefa71b168'
         '5e0b3d08943d934ace681da78f3bc5f2'
         '9b271c57fb749833e4163e11b11a52af')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cp $srcdir/$pkgname-$pkgver.config .config
  patch -Np1 -i $srcdir/$pkgname-$pkgver-cpio-mkdir.u || return 1
  patch -Np1 -i $srcdir/$pkgname-$pkgver-tar.u || return 1
  patch -Np1 -i $srcdir/$pkgname-$pkgver-stat.u || return 1
  patch -Np1 -i $srcdir/$pkgname-$pkgver-ris.u || return 1
  patch -Np1 -i $srcdir/$pkgname-$pkgver-depmod.u || return 1
  patch -Np1 -i $srcdir/$pkgname-$pkgver-zmodules.u || return 1
  patch -Np1 -i $srcdir/$pkgname-$pkgver-usage.u || return 1
  
  # if you want to run menuconfig uncomment the following line:
  #make menuconfig ; return 1
  if [ "$(gcc --version | awk '{ print $3; exit }')" == "4.5.0" ]; then
      # "CFLAGS=-O0" is a workaround for GCC 4.5.0 (sed crach)
      # "CFLAGS=-fno-tree-pta" may be a workaround for GCC 4.5.0 (sed garbage)
      make -j 4 "CFLAGS=-O0" && make "CFLAGS=-O0" install
  else
      make -j 4 && make install
  fi
  chmod 4755 _install/bin/busybox
}

package() {
  cp -a $srcdir/$pkgname-$pkgver/_install/* $pkgdir
  rm -f $pkgdir/sbin/depmod $pkgdir/usr/bin/ar
  # Busybox config files
  mkdir -p $pkgdir/etc
  cp $srcdir/busybox.conf $pkgdir/etc
  chmod 600 $pkgdir/etc/busybox.conf
  cp $srcdir/dnsd.conf $pkgdir/etc
  cp $srcdir/udhcpd.conf $pkgdir/etc
  touch $pkgdir/etc/resolv.conf
  cp $srcdir/inetd.conf $pkgdir/etc
  mkdir -p $pkgdir/etc/init.d
  cp $srcdir/daemon $pkgdir/etc/init.d
      for i in crond dnsd ftpd httpd inetd klogd ntpd syslogd telnetd tftpd \
	     udhcpd zcip ; do
      ln -s $i $pkgdir/etc/init.d/$i
  done
  rm $pkgdir/linuxrc
  mkdir -p $pkgdir/etc/modprobe.d
  # Udhcpd stuff
  mkdir -p $pkgdir/usr/share/udhcpc
  cp $srcdir/udhcp.script $pkgdir/usr/share/udhcpc/default.script
  chmod +x $pkgdir/usr/share/udhcpc/default.script
  # ZeroConf stuff.
  cp $srcdir/zcip.script $pkgdir/etc
}
